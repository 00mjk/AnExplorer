apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId "dev.dworks.apps.anexplorer"
        minSdkVersion 14
        targetSdkVersion 21
    }
    signingConfigs {
        try {
            release {
                def Properties localProps = new Properties()
                localProps.load(new FileInputStream(file('../default.properties')))
                def Properties keyProps = new Properties()
                if (localProps['keystore.props.file'] != null) {
                    keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
                }
                storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
                keyAlias keyProps["alias"]
                storePassword keyProps["storePass"]
                keyPassword keyProps["pass"]
            }
        }
        catch (java.lang.Exception e){ }
    }
    buildTypes {
        release {
            proguardFiles 'proguard/proguard-project.pro',
                    'proguard/proguard-guava.pro',
                    'proguard/proguard-google-play-services.pro'
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
        }
    }
    flavorDimensions   "release", "default"
    productFlavors {
        free {
            applicationId "dev.dworks.apps.anexplorer"
            versionName "2.7"
            versionCode = 42
            flavorDimension "default"
        }
        pro {
            applicationId "dev.dworks.apps.anexplorer.pro"
            versionName "2.7"
            versionCode = 42
            flavorDimension "default"
        }
        play {
            flavorDimension "release"
        }
        other {
            flavorDimension "release"
        }
    }
    lintOptions {
        abortOnError false
    }

    aaptOptions {
        noCompress 'apk'
    }
}

dependencies {
    compile files('libs/RootTools.jar')
    compile 'com.android.support:support-v4:+'
    compile 'com.android.support:appcompat-v7:+'
    compile 'com.github.mrengineer13:snackbar:+'
    compile 'com.google.guava:guava:18.0'
    freeCompile 'com.crashlytics.android:crashlytics:+'
    freeCompile 'com.google.android.gms:play-services-ads:+'
    otherCompile 'com.crashlytics.android:crashlytics:+'
    otherCompile 'com.google.android.gms:play-services-ads:+'
}