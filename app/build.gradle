apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId "dev.dworks.apps.anexplorer"
        versionName "3.1"
        versionCode = 47
        minSdkVersion 16
        targetSdkVersion 23
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources true
            proguardFiles 'proguard/proguard-project.pro',
                    'proguard/proguard-guava.pro',
                    'proguard/proguard-google-play-services.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard/proguard-project.pro',
                    'proguard/proguard-guava.pro',
                    'proguard/proguard-google-play-services.pro'
        }
    }
    flavorDimensions "release", "default"
    productFlavors {
        free {
            dimension "default"
        }
        pro {
            applicationId "dev.dworks.apps.anexplorer.pro"
            dimension "default"
        }
        play {
            dimension "release"
        }
        other {
            dimension "release"
        }
    }
    lintOptions {
        abortOnError false
    }
    aaptOptions {
        noCompress 'apk'
    }
}

ext {
    supportLibVersion = '24.0.0'
    gmsVersion = '9.0.1'
}

dependencies {
    compile files('libs/RootTools.jar')
    compile 'com.android.support:support-v4:+'
    compile 'com.android.support:appcompat-v7:+'
    compile 'com.android.support:design:+'
    compile 'com.google.guava:guava:18.0'
    compile 'com.jaredrummler:android-processes:+'
    freeCompile "com.google.firebase:firebase-core:${gmsVersion}"
    freeCompile "com.google.firebase:firebase-crash:${gmsVersion}"
    freeCompile "com.google.firebase:firebase-ads:${gmsVersion}"
    otherCompile "com.google.firebase:firebase-core:${gmsVersion}"
    otherCompile "com.google.firebase:firebase-crash:${gmsVersion}"
    otherCompile "com.google.firebase:firebase-ads:${gmsVersion}"
}

def props = new Properties()
props.load(new FileInputStream(rootProject.file("keystore.properties")))
android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
android.signingConfigs.release.storePassword props.keyStorePassword
android.signingConfigs.release.keyAlias props.keyAlias
android.signingConfigs.release.keyPassword props.keyAliasPassword

apply plugin: 'com.google.gms.google-services'